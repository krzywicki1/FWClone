plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.6'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations { compileOnly { extendsFrom annotationProcessor } }

repositories { // Search for everything but artifacts with group starting with "org.zkoss"
    mavenCentral {
        content { excludeGroupByRegex "org\\.zkoss.*" }
    }
    maven { // ZK CE, only artifacts with group 'org.zkoss'
        url "https://mavensync.zkoss.org/maven2"
        content { includeGroupByRegex("org\\.zkoss.*")  } // repo content filter
    }
    maven { // ZK EE, only artifacts with group 'org.zkoss'
        url "https://mavensync.zkoss.org/eval"
        content { includeGroupByRegex("org\\.zkoss.*") } // repo content filter
    }
}

def ver = [ zk:"9.6.3-Eval", zksbs:"2.5.12", jquery:"3.6.1", bootstrap:"5.2.3" ]

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Web Frameworks
    //noinspection VulnerableLibrariesLocal
    implementation "org.webjars:jquery:${ver.jquery}"
    implementation "org.webjars:bootstrap:${ver.bootstrap}"
    implementation "org.webjars:webjars-locator:0.46"
    // ZK Framework
    implementation "org.zkoss.zkspringboot:zkspringboot-starter:${ver.zksbs}"
    implementation "org.zkoss.zk:zkbind:${ver.zk}"
    implementation "org.zkoss.zk:zhtml:${ver.zk}"
    implementation "org.zkoss.zk:zkplus:${ver.zk}"
    // ZK EE
    implementation "org.zkoss.zk:zkmax:${ver.zk}"
    implementation "org.zkoss.zk:zuti:${ver.zk}"
    implementation "org.zkoss.zk:zml:${ver.zk}"
    //noinspection VulnerableLibrariesLocal
    implementation 'net.sf.jasperreports:jasperreports:6.20.0'
    //noinspection VulnerableLibrariesLocal
    implementation 'com.lowagie:itext:2.1.7'
    implementation 'org.jetbrains:annotations:23.0.0'

    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'com.h2database:h2'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') { useJUnitPlatform() }
